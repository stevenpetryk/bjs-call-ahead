
$(document).ready ->
	# Set up jQuery to use text/javascript requests only
	jQuery.ajaxSetup({ 
	  'beforeSend': (xhr) ->
	  	xhr.setRequestHeader("Accept", "application/json")
	})

	# Initialize view
	View.init()

View = 
	# Pattern for disclaimer matching
	disclaimerPattern: /\ \[\*\=(.+)\]/

	# Initializes the view in its entirety
	init: ->
		# Initialize viewport height
		@fixInfinityEdges()

		# Load the first category
		# Controller.loadCategory(1)

		# Bind events
		@bindAll()

	# Binds all events
	bindAll: ->
		# Switching between categories
		###$('.category').click (e) ->
			e.preventDefault()

			category = $(this).attr('id').split('_')[1]

			Controller.loadCategory( category )###

		# Clicking on a favorite star
		$('#menu li .favorite_toggle').live 'click', ->
			itemid = $(this).closest('li').data('itemid')

			Controller.setFavorite(itemid, !$(this).data('checked'))

		# Window resizing
		$(window).resize @fixInfinityEdges
	
	# Populates the central menu with a supplied list
	populateMenu: (list) ->
		menu = $('#menu')

		ul = $('<ul>')

		# Iterate through list, generating HTML
		$.each(list, (k, item) ->
			li = $('<li>').attr('id', 'item_'+item.id)
			li.data('itemid', item.id)

			# Process item name with disclaimers
			item.name = item.name.replace(
				View.disclaimerPattern, 
				' <span class="tooltip" data-tooltip="$1">*</span>'
			)

			# Header
			hDiv = $('<div>').addClass('header').appendTo(li)
			$('<h1>').html(item.name)
				.appendTo(hDiv)

			# Price tag
			pricetag = $('<p class="price">')
			pricetag.html('$'+item.price.toFixed(2))
			pricetag.prepend(
					# Favorite toggle "checkbox"
					$('<a class="favorite_toggle">').append(
						$('<img>').attr 'src', ->
							if item.favorite 
							then "<%= asset_path 'favorited.png' %>"
							else "<%= asset_path 'notfavorited.png' %>"
					).data 'checked', item.favorite
			).appendTo(hDiv)


			# Description
			$('<p class="desc">').html(item.description)
				.appendTo(li)

			# aaaaand append it all.
			ul.append(li)
		)

		menu.html(ul)

	# Highlights current #category li
	highlightCategory: (id) ->
		$('.category').parent().removeClass('current')
		$('#category_'+id).parent().addClass('current')

	# Resizes certain elements to stay at 100% height as the page height changes
	fixInfinityEdges: ->
		elements = $('#categories, #menu, #order')
		idealHeight = 
			$(window).height() - ( $('#footer').height() + $('#header').height() )
		elements.height( idealHeight )

	# Toggles the given favorite star
	toggleStar: (itemid, toggle) ->
		srcs =
			'true' : '<%= asset_path 'favorited.png' %>'
			'false': '<%= asset_path 'notfavorited.png' %>'

		$('#item_'+itemid).find('.favorite_toggle')
			.data('checked', toggle)

		$('#item_'+itemid).find('.favorite_toggle img')
			.attr('src', srcs[toggle.toString()])

Controller = 
	loadCategory: (id) ->
		Model.findCategory id, (data) ->
			View.populateMenu(data)
			View.highlightCategory(id)

	setFavorite: (itemid, toggle) ->
		srcs = {
			'true' : '<%= asset_path 'favorited.png' %>'
			'false': '<%= asset_path 'notfavorited.png' %>'
		}

		if toggle
			Model.addFavorite(itemid)
		else
			Model.removeFavorite(itemid)

		View.toggleStar(itemid, toggle)

Model = 
	findCategory: (id, callback) ->
		url = 
			if id == 'favorites' 
				'/favorites'
			else
				'/categories/'+id+'/items'

		$.getJSON url, (data) ->
			callback(data)

	addFavorite: (id) ->
		$.post '/favorites',
			'item_id': id

	removeFavorite: (id) ->
		$.ajax
        url: '/favorites/'+id,
        type: 'post',
        dataType: 'script',
        data: { '_method': 'delete' }
